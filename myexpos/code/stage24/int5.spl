alias user_sp R1;
user_sp = SP;

alias ret_addr R2;
ret_addr = [PTBR + 2*(user_sp-1)/512]*512 + (user_sp-1)%512;

alias syscall_num R3;
syscall_num = [[PTBR + 2*(user_sp-5)/512]*512 + (user_sp-5)%512];

alias arg1 R4;
arg1 = [[PTBR + 2*(user_sp-4)/512]*512 + (user_sp-4)%512];

alias arg2 R5;
arg2 = [[PTBR + 2*(user_sp-3)/512]*512 + (user_sp-3)%512];

alias pid R6;
pid = [SYSTEM_STATUS_TABLE + 1];

[PROCESS_TABLE + 16*pid + 9] = syscall_num;
SP = [PROCESS_TABLE + 16*pid + 11]*512 - 1;

//----------------------fn definition----------------------------//

if(syscall_num == 2) then  //Open
    [PROCESS_TABLE + 16*pid + 13] = user_sp;

    R7 = [PROCESS_TABLE + 16*pid + 11]*512 + RESOURCE_TABLE_OFFSET;

    R8 = 0;
    while(R8 < 8) do
        if([R7 + 2*R8 + 0] == -1) then
            break;
        endif;

        R8 = R8 + 1;
    endwhile;

    if(R8 >= 8) then
        [ret_addr] = -3;
        goto return_to_user;
    endif;

    multipush(R1, R2, R3, R4, R5, R6, R7, R8);
    R1 = 3;  //open
    R2 = arg1;
    call FILE_MANAGER;
    multipop(R1, R2, R3, R4, R5, R6, R7, R8);

    if(R0 == -1 || R0 == -2) then
        [ret_addr] = R0;
        goto return_to_user;
    endif;

    [R7 + 2*R8 + 0] = FILE;
    [R7 + 2*R8 + 1] = R0;

    [ret_addr] = R8;
    goto return_to_user;
endif;


if(syscall_num == 3) then //close
    [PROCESS_TABLE + 16*pid + 13] = user_sp;
    
    alias resource_table R7;
    resource_table = [PROCESS_TABLE + 16*pid + 11]*512 + RESOURCE_TABLE_OFFSET;

    if(arg1 < 0 || arg1 >= 8 || [R7 + 2*arg1 + 0] != FILE) then
        [ret_addr] = -1;
        goto return_to_user;
    endif;
    
    alias open_file_table_entry R8;
    R8 = [resource_table + 2*arg1 + 1];

    multipush(R1, R2, R3, R4, R5, R6, R7, R8);
    R1 = 4;  //close
    R2 = open_file_table_entry;
    call FILE_MANAGER;
    multipop(R1, R2, R3, R4, R5, R6, R7, R8);
    
    [resource_table + 2*arg1 + 0] = -1;
    [resource_table + 2*arg1 + 1] = -1;

    [ret_addr] = -1;
    goto return_to_user;
endif;

return_to_user:
    [PROCESS_TABLE + 16*pid + 9] = 0;
    SP = user_sp;
    ireturn;

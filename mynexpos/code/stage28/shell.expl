int main()
{
decl
    int temp, pid, a, flag, retcom;
    str input, username, password;
enddecl

begin
    while(1 == 1) do
        write("#Shell $");
        read(input);

        flag=0;
        if(input == "Logout" OR input == "Shutdown") then
            flag=1;
        endif;

        if(input == "Remusr" OR input == "Getuid" OR input == "Getuname") then
            flag=1;
            write("Username ");
            read(username);
        endif;

        if(input == "Newusr" OR input == "Setpwd") then
            flag=1;
            write("Username ");
            read(username);
            write("Password ");
            read(password);
        endif;

        if(flag==1) then
            retcom = exposcall(input, username, password);
            if(retcom < 0) then
                write("BAD COMMAND");
            else
                if(input == "Getuid" OR input == "Getuname") then
                    write(retcom);
                endif;
            endif;
        else
            pid = exposcall("Fork");
            if(pid < 0) then
                temp = exposcall("Write",-2, "Fork Fail");
                continue;
            endif;

            if(pid != 0) then
                temp = exposcall("Wait",pid);
            else
                temp = exposcall("Exec",input);
                if(temp != 0) then
                    temp = exposcall("Write",-2, "BAD COMMAND");
                    temp = exposcall("Exit");
                endif;
            endif;
        endif;
    endwhile;

    return 0;
end
}

